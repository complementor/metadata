import aQute.bnd.version.MavenVersion
import me.champeau.gradle.ArtifactJapicmpTask
import org.semanticweb.owlapi.gradle.osgi.BundleTask
import org.semanticweb.owlapi.gradle.osgi.OSGIWrapTask

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }

    }
    dependencies {
        classpath fileTree('buildSrc/lib/')
        classpath 'me.champeau.gradle:japicmp-gradle-plugin:latest.integration'
        classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:4.1.0'
    }
}

plugins {
  id 'com.github.ben-manes.versions' version '0.20.0'
}

allprojects {
    apply plugin: 'maven'
    apply plugin: 'eclipse'

    group = 'net.sourceforge.owlapi'
    version = '6.0.0-SNAPSHOT'
    ext {
        baselineVersion = '5.1.11'
    }

}

subprojects {
    repositories {
        mavenLocal()
        mavenCentral()
//        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }

    apply plugin: 'java'
    sourceCompatibility = 11
    targetCompatibility = 11
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
    apply plugin: 'me.champeau.gradle.japicmp'

    task packageSources(type: Jar) {

        classifier = 'sources'
        from sourceSets.main.allSource
    }
    task packageJavadoc(type: Jar) {
        dependsOn javadoc
        classifier = 'javadoc'
        from javadoc.outputs
    }
    artifacts.archives packageSources
    artifacts.archives packageJavadoc

    dependencies {
        compile 'com.google.code.findbugs:jsr305:[2.0.0,4)'
        compile 'org.slf4j:slf4j-api:[1.8.0-beta2,)'
        testCompile 'org.hamcrest:hamcrest-core:[1.3,2)'
        testCompile 'junit:junit:[4.12,5)'
        testCompile 'org.mockito:mockito-core:[2.18.3,3)'
        testCompile 'org.slf4j:slf4j-simple:[1.8.0-beta2,)'
    }

    task wrap(type: OSGIWrapTask) {
        dependsOn compileJava
        configuration = project.configurations.compile
        copyExistingBundles=true
        walkExistingBundles=true
    }
    jar {
        from(sourceSets.main.allSource) {
            include "**/packageinfo"
        }
    }
    classes {
        copy {
            into sourceSets.main.java.outputDir
            from sourceSets.main.allSource
            include "**/packageinfo"
        }
    }


    task bundle(type: BundleTask) {
        dependsOn jar
        dependsOn wrap
        bundleClasspath = project.fileTree(wrap.bundleOutputDir).files
        //classifier="osgi"
        
        manifest {
            attributes('Export-Package': "{local-packages}",


                    "Import-Package": "com.github.benmanes.*;resolution:=optional,*",

                    "Bundle-SymbolicName": "${project.group}.${project.name}"

            )
        }
    }

    task initPackageInfo {
        doFirst {
            sourceSets.main.allJava*.parentFile.unique().each {
                File packageInfoFile = new File(it, "packageinfo")
                if (!packageInfoFile.exists()) {
                    println "iPi ${it}"
                    def out = packageInfoFile.newPrintWriter()
                    MavenVersion v = MavenVersion.parseMavenString(project.version.toString())
                    out.println "version 6"
                    out.close()
                }

            }
        }
    }
    if (!(project.name in ["benchmarks"])) {
        task japicmp(type: ArtifactJapicmpTask) {
            description "compare project api to baselineVersion"
            group "verification"
            baseline = "${project.group}:${project.name}:${project.baselineVersion}"
            to = project.jar.archivePath
            ignoreAllMissingClasses = true
            noAnnotations = true
            onlyModified = true
            failOnModification = false
            htmlOutputFile = file("$buildDir/reports/japi.html")
        }
    }
}
